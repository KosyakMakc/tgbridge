plugins {
    id "java"
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.github.johnrengelman.shadow"
}

repositories {
    maven { url = 'https://maven.neoforged.net/releases' }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    forge "net.neoforged:forge:${rootProject.minecraft_version}-${rootProject.neo_version}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"

    implementation "thedarkcolour:kotlinforforge:${rootProject.forge_kotlin_version}"

    shadow implementation(project(":common"))
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    dependsOn 'processResources'
    finalizedBy 'remapJar'

    configurations = [project.configurations.shadow]
}

remapJar {
    inputFile = shadowJar.archiveFile
    archiveFileName = "tgbridge-forge-${rootProject.version}.jar"
}

artifacts {
    archives remapJar
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
