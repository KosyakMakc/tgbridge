plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = rootProject.maven_group
    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version

    base {
        archivesName = rootProject.archives_base_name
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        if (project.name == "paper") {
            implementation "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.kotlin_version}"
            implementation "org.jetbrains.kotlin:kotlin-reflect:${rootProject.kotlin_version}"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.5.1"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1"
        } else {
            compileOnly "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.kotlin_version}"
            compileOnly "org.jetbrains.kotlin:kotlin-reflect:${rootProject.kotlin_version}"
            compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2"
            compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-core:1.5.1"
            compileOnly "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1"
        }
        implementation("dev.inmo:tgbotapi:9.0.0") {
            exclude(module: 'kotlin-stdlib')
            exclude(module: 'kotlin-reflect')
            exclude(module: 'kotlinx-coroutines-core')
            exclude(module: 'kotlinx-serialization-core')
            exclude(module: 'kotlinx-serialization-json')
            exclude(module: 'slf4j-api')
        }
        api "net.kyori:adventure-api:4.12.0"
        api("net.kyori:adventure-text-serializer-gson:4.12.0") {
            exclude(module: 'gson')
        }
        compileOnly "com.google.code.gson:gson:2.10.1"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        it.options.release = 17
    }

    compileKotlin {
        kotlinOptions.jvmTarget = 17
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}
