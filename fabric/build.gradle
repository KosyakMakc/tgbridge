plugins {
	id "java"
	id "dev.architectury.loom" version "1.3-SNAPSHOT"
	id "org.jetbrains.kotlin.jvm"
	id "org.jetbrains.kotlin.plugin.serialization"
	id "com.github.johnrengelman.shadow"
}

repositories {}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"

	modImplementation include("net.kyori:adventure-platform-fabric:${rootProject.adventure_fabric_version}")

	shadow implementation(project(":common"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

jar.enabled = false // Replaced by "shadowJar"

shadowJar {
	dependsOn 'processResources'
	finalizedBy 'remapJar'

	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir

	configurations = [project.configurations.shadow]
	archiveClassifier = jar.archiveClassifier
	destinationDirectory = jar.destinationDirectory
}

remapJar {
	inputFile = shadowJar.archiveFile
	archiveFileName = "tgbridge-fabric-${rootProject.version}.jar"
}

artifacts {
	archives remapJar
}
