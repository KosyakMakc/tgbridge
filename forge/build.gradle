plugins {
    id "dev.architectury.loom" version "1.2-SNAPSHOT"
}

repositories {
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"

    implementation "thedarkcolour:kotlinforforge:${rootProject.forge_kotlin_version}"

    implementation project(":common")
    shadow project(":common")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    archiveFileName = "tgbridge-forge-${rootProject.version}.jar"

    configurations = [project.configurations.shadow]
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
